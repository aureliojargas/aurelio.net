# SED quiZZ data file
# 20010815 <verde@verde666.org> 

# TIPS:
* responda como se a opção -n não existisse (sempre imprime)
* não colocar espaços em branco entre comandos

### endereçamento
apagar a primeira linha
  1d
  2,$!d
mostrar apenas a primeira linha
  q
  1q
  1!d
  2,$d
apagar a última linha
  $d
apagar as três primeiras linhas
  1,3d
  4,$!d
mostrar apenas as sete primeiras linhas
  7q
  1,7!d
  8,$d
apagar todas as linhas em branco
  /^$/d
  /./!d
apagar da linha "verde" até a última linha
  /verde/,$d
imprimir apenas a linha 25
  25!d
  25q;d
  1,24d;25,4d
imprimir até a primeira linha em branco
  /^$/q
  1,/^$/!d
apagar o bloco de linhas entre "verde" e "amarelo", inclusive
  /verde/,/amarelo/d
mostrar apenas linhas que contenham números
  /[0-9]/!d
  /^[^0-9]*$/d
apagar as linhas que contenham "água"
  /água/d
substituir oi por tchau na linha 3
  3s/oi/tchau/
# pegadinhas de escapes
remover todos os colchetes []
  s/[][]//g
  s/\[//g;s/]//g
  s/\[//g;s/\]//g
apagar todos os trechos entre parênteses (), inclusive
  s/([^)]*)//g
colocar todos os números entre colchetes []
  s/[0-9]\+/[&]/g
  s/[0-9][0-9]*/[&]/g
# começo e fim de linha
apagar as 3 últimos caracteres da linha
  s/...$//
  s/.\{3\}$//
  s/\(.*\)...$/\1/
mostrar apenas os 3 últimos caracteres da linha
  s/.*\(...\)$/\1/
  s/.*\(.\{3\}\)$/\1/
apagar os 3 primeiros caracteres da linha
  s/^...//
  s/^.\{3\}//
  s/^...\(.*\)/\1/
mostrar apenas os 3 primeiros caracteres da linha
  s/^\(...\).*/\1/
  s/^\(.\{3\}\).*/\1/
inserir um # no começo de cada linha
  s/^/#/
  s/.*/#&/
mostrar linhas que começam com letras maiúsculas
  /^[A-Z]/!d
  /^[^A-Z]/d
colocar um @ no final de linhas que começam com "email"
  /^email/s/$/@/
  s/^email.*/&@/
apagar o 5º caractere de cada linha
  s/.//5
  s/^\(....\)./\1/
  s/^\(.\{4\}\)./\1/
# espaço reserva
guardar as linhas "fone:" no espaço reserva
  /fone:/H
guardar a primeira linha "fone:" no espaço reserva
  1,/fone:/{/fone:/h;}
trocar a linha 25 pelo conteúdo do espaço reserva
  25g
colocar no final da linha 25 o conteúdo do espaço reserva
  25{G;s/\n//;}
mostrar linhas com mais de 40 caracteres
  /^.\{41\}/!d
apagar linhas com mais de 40 caracteres
  /^.\{41\}/d
inverter de lugar todos os números entre dois-pontos (n:m -> m:n)
  s/\([0-9]\+\):\([0-9]\+\)/\2:\1/
trocar "verde" ou "amarelo" por "azul", sempre
  s/verde\|amarelo/azul/g
  s/verde/azul/g;s/amarelo/azul/g
substituir o primeiro "verde" por "amarelo", menos nas linhas que contém "azul"
  /azul/!s/verde/amarelo/
trocar vários espaços em branco seguidos por um só
  s/ \+/ /g
  s/  */ /g
mostrar o número de linhas do arquivo
  $=;d
incrementar em uma unidade cada dígito de um número
  y/0123456789/1234567890/
apagar a 4ª ocorrência de "hoje" em cada linha
  s/hoje//4
juntar as 4 primeiras linhas numa só, apagando a quebra de linha
  1{N;N;N;s/\n//g;}
  1,3{N;s/\n//;}'
