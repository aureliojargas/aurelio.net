---
title: Sed salva o dia novamente
tags: [livro, programador, shell, regex]
hide_ads: true
worked: 4:00
---

J√° perdi a conta de quantas vezes o [sed](/sed/) me salvou.

Ele √© [meu comando preferido do Unix](/blog/2009/07/03/10anos-sed/). √â simples, direto, minimalista, poderoso, enigm√°tico. E claro, faz uso intensivo de [express√µes regulares](/regex/), que √© minha outra paix√£o.

P√≥rem, esta aparente simplicidade esconde um programa muito poderoso, que pode fazer as mais complicadas edi√ß√µes em textos, de maneira automatizada. Precisou substituir, apagar, inserir, juntar, quebrar, inverter, duplicar um texto? Sed nele!

> Tamb√©m d√° pra fazer [jogos com o sed](/projects/sedsokoban/), se voc√™ for desocupado o suficiente ;)

Neste s√°bado, precisei do sed mais uma vez.

A 5¬™ edi√ß√£o do [livro Express√µes Regulares](http://piazinho.com.br) ser√° lan√ßada daqui alguns dias, e a grande novidade desta vez √© que al√©m da vers√£o impressa, tamb√©m ter√° a vers√£o digital (ebook). **Finalmente!** üéâ

Por√©m, quando fui testar o ebook que a editora gerou, todos os c√≥digos-fonte listados no livro apareceram sem alinhamento (indent), com todas as linhas come√ßando no canto esquerdo :(

![](/img/blog/ebook-align.png)

Os fontes originais do livro est√£o no InDesign, que √© o programa que a editora usa para a diagrama√ß√£o, e foram exportados para o [formato EPUB](https://en.wikipedia.org/wiki/EPUB) para gerar o ebook. Felizmente, o formato EPUB √© edit√°vel, pois nada mais √© do que um arquivo ZIP com arquivos XHTML e CSS dentro, ent√£o o descompactei e fui fu√ß√°-lo.

Aqui est√° o trecho XHTML que gera o c√≥digo da foto anterior:

```html
<p class="_CodigoFontePrimLin1"># Cada dom√≠nio est√° numa subpasta em /sites</p>
<p class="_CodigoFonte1">server {</p>
<p class="_CodigoFonte1">    server_name ~ "^(www\.)?(?&lt;dominio&gt;.+)$";
</p>
<p class="_CodigoFonte1">    location / {</p>
<p class="_CodigoFonte1">        root /sites/$dominio;</p>
<p class="_CodigoFonte1">    }</p>
<p class="_CodigoFonte1">}</p>
```

Como deveria ser:

```html
<pre>
# Cada dom√≠nio est√° numa subpasta em /sites
server {
    server_name ~ "^(www\.)?(?&lt;dominio&gt;.+)$";
    location / {
        root /sites/$dominio;
    }
}
</pre>
```

Por algum motivo sobrenatural, cada linha do c√≥digo-fonte virou um par√°grafo diferente (tag `<p>‚Ä¶</p>`), em vez de tudo ser um √∫nico bloco com a tag `<pre>‚Ä¶</pre>`.

Se fosse poucos casos eu at√© arrumaria na m√£o mesmo, mas contei aqui (na verdade, o `grep | wc -l` contou) e s√£o 340 blocos de c√≥digo a serem arrumados no total. √â, vai ter que rolar um sed macho...

Vamos analisar o formato geral dos blocos de c√≥digo:

```html
qualquer coisa antes
<p class="_CodigoFontePrimLin1">primeira linha do bloco</p>
<p class="_CodigoFonte1">segunda linha do bloco</p>
<p class="_CodigoFonte1">terceira linha do bloco</p>
...
<p class="_CodigoFonte1">√∫ltima linha do bloco</p>
qualquer coisa depois
```

O maior problema aqui √© identificar o in√≠cio e o fim dos blocos. O in√≠cio tudo bem, est√° identificado pela classe `_CodigoFontePrimLin1`. J√° o final, n√£o possui uma marca√ß√£o especial. Eu s√≥ sei que terminou o bloco de c√≥digo quando aparece uma linha que **n√£o tem** a classe `_CodigoFonte1`.

Se tivesse uma marca√ß√£o clara de in√≠cio e fim, seria bem f√°cil. O sed j√° suporta nativamente esta nota√ß√£o, basta colocar os padr√µes entre barras, antes dos comandos, assim:

```bash
sed '/in√≠cio/,/fim/ { comandos; }'
```

Como n√£o posso usar isso, pois n√£o tenho um padr√£o claro de t√©rmino do bloco, terei que usar outra t√°tica: identificar o in√≠cio do bloco, entrar num loop que vai lendo as pr√≥ximas linhas e s√≥ sair do loop quando encontrar uma linha que n√£o fa√ßa parte do bloco. Esse √© o esqueleto do comando:

```bash
sed '
  /in√≠cio/ {

    # insere a tag <pre> no in√≠cio

    # inicia o loop
      # remove as tags <p> e </p>
      # l√™ a pr√≥xima linha
      # continua no loop se for uma linha do bloco

    # insere a tag </pre> no final
  }
'
```

O primeiro passo √© fazer o padr√£o que identifica o √≠nicio dos blocos. J√° vimos que a classe `_CodigoFontePrimLin1` foi usada no in√≠cio deles. Por√©m, vai que algum bloco est√° marcado errado e n√£o usou essa classe? Bem, eu sei com certeza que todas as linhas de c√≥digo possuem uma classe cujo nome come√ßa com `_CodigoFonte`, ent√£o vou preferir usar esse padr√£o mais abrangente para n√£o deixar de fora nenhum bloco.

```
/_CodigoFonte/ {

  # insere a tag <pre> no in√≠cio

  # inicia o loop
    # remove as tags <p> e </p>
    # l√™ a pr√≥xima linha
    # continua no loop se for uma linha do bloco

  # insere a tag </pre> no final
}
```

Agora vamos para o loop. Al√©m de montar a estrutura do loop, o que eu tenho que fazer em todas as linhas do bloco, √© apagar as tags `<p>` e `</p>` feiosas. Farei isso em dois passos, com dois `s///` para ficar mais simples a express√£o regular.

```
/_CodigoFonte/ {

  # insere a tag <pre> no in√≠cio

  # inicia o loop
  :meuloop

    # remove as tags <p> e </p>
    s/^[[:blank:]]*<p [^>]*>//
    s/[[:blank:]]*<\/p>$//

    # l√™ a pr√≥xima linha
    n

    # continua no loop se for uma linha do bloco
    /_CodigoFonte/ b meuloop

  # insere a tag </pre> no final
}
```

O loop em sed √© feito marcando um ponto qualquer no script com o comando `:` seguido de um nome (no exemplo usei `:meuloop`) e depois voc√™ usa o comando `b` (de Branch), para pular para o ponto marcado (sim, √© um [GOTO](https://en.wikipedia.org/wiki/Goto)). S√≥ perceba que meu comando `b` n√£o √© incondicional, ele s√≥ √© aplicado se a linha atual tiver o padr√£o `_CodigoFonte`. Esse √© o jeit√£o do sed de fazer um `if`.

Ent√£o beleza, nesse ponto meu script j√° identifica os blocos de c√≥digo-fonte e apaga todas as tags `<p>` dentro deles. As outras linhas do livro permanecem intactas. O que est√° faltando agora √© inserir a tag `<pre>` no in√≠cio e a `</pre>` no fim do bloco. Isso √© f√°cil com o comando `i` (de Insert). A sintaxe dele √© estranha, exigindo uma quebra de linha escapada, mas funciona.

```
/_CodigoFonte/ {

  # insere a tag <pre> no in√≠cio
  i \
<pre>

  # inicia o loop
  :meuloop

    # remove as tags <p> e </p>
    s/^[[:blank:]]*<p [^>]*>//
    s/[[:blank:]]*<\/p>$//

    # l√™ a pr√≥xima linha
    n

    # continua no loop se for uma linha do bloco
    /_CodigoFonte/ b meuloop

  # insere a tag </pre> no final
  i \
</pre>

}
```

E pronto!

O sed √© extremamente r√°pido. Num piscar de olhos ele vai editar o livro todo, arrumando todos os blocos. A√≠ √© s√≥ compactar tudo de volta e o arquivo EPUB estar√° do jeito que Jesus gosta: com todos os c√≥digos-fonte bem alinhados :)

Claro que a ‚Äúvida real‚Äù √© mais dura do que isso, e tive que fazer mais alguns sed para lidar com falsos-positivos, trocar tabs por espa√ßos, remover tags vazias de √≠ndice remissivo e fazer ajustes no arquivo CSS.

Veja como ficou o script completo:

- https://github.com/aureliojargas/ebook-fix/blob/master/ebook-fix.sh
